Lab4:IR 实验要求

本次实验无需提交实验报告

- 本实验保证输入的正确性 
- 若无特殊说明，则本实验及后续实验需要翻译的内容的语义均与C语言保持一致 
- 请删除关于 LLVM 的所有无用的 import ，服务器只维持了能在服务器环境下生成 LLVM IR 的最小 jar 包集合，如果你 import 了不相关的文件，可能会导致 Fail to make 
- 你的 main 函数应该接受两个参数，第一个参数是源文件路径，第二个参数是生成的 LLVM IR 存放的文件路径

实验开始前

请切换git分支至lab4。

本次实验以及后续涉及中间代码的实验，需要附加 7 个 jar 包，其中分为核心包与平台包。核心包为：

    llvm-13.0.1-1.5.7.jar
    llvm-platform-13.0.1-1.5.7.jar
    javacpp-1.5.7.jar
    llvm4j-0.1.1-SNAPSHOT.jar
    kotlin-stdlib-1.9.0.jar

平台包以 linux-x86_64 系统为例，为：

    llvm-13.0.1-1.5.7-linux-x86_64.jar
    javacpp-1.5.7-linux-x86_64.jar

有两种导入上述 jar 包的方法，你可以选择任意一种，选择何种方式不会影响 oj 的判定。

方法一：

在👉南大云盘👈下载以上 jar 包，平台包请选择对应系统下载。

（make用户）将下载好的 7 个 jar 包放在 /usr/local/lib 目录下，并自行修改 Makefile ，修改时注意本次实验接受两个参数。

（IntelliJ IDEA用户）将下载好的 7 个 jar 包放在实验根目录下的 /lib 目录下，并将 /lib 目录添加为library root。注意，请务必将 /lib 目录下的所有文件都加入 .gitignore，以免产生的 submit.zip 过大而无法上传oj！

方法二：

使用 Maven 导入这些 jar 包，在项目根目录新建 pom.xml 配置文件，向文件中添加以下内容。

    <!-- 镜像仓库地址 -->
	<repositories>
        <repository>
            <id>jitpack.io</id>
            <url>https://jitpack.io</url>
        </repository>
        <repository>
            <id>snapshots</id>
            <url>https://s01.oss.sonatype.org/content/repositories/snapshots/</url>
        </repository>
    </repositories>
    <!-- 依赖项 -->
	<dependencies>
        <dependency>
            <groupId>com.github.chaos-warzh</groupId>
            <artifactId>llvm4j</artifactId>
            <version>0.1.1-SNAPSHOT</version>
        </dependency>
        <dependency>
            <groupId>org.antlr</groupId>
            <artifactId>antlr4-runtime</artifactId>
            <version>4.9.1</version>
        </dependency>
    </dependencies>

注意这只是需要添加的依赖，pom.xml 的其他部分需要根据个人的实际配置情况补全，并且请勿随意使用 LLVM 和 ANTLR 的其他版本，以免出现不可预料的问题。在项目根目录运行 mvn install 即可安装以上依赖。
在 Maven 的默认配置下你可以在 ~/.m2/repository/ 路径下找到导入的 jar 包。

# 实验输入

第一个参数是SysY源文件路径，第二个参数是生成的 LLVM IR 存放的文件路径。

## 实验内容

本次实验内容为翻译中间代码，你需要将SysY语言源码正确翻译为LLVM IR。

## Part1 翻译 main 函数和表达式

- 该部分需要完成对main函数和表达式的翻译，要求如下:
```
        main 函数无参数且返回值为int类型
        main 函数仅包含return语句
        在return时不仅仅会返回一个单独的数字，而是返回一个常量表达式
        常量表达式仅包含整型字面值（十进制、八进制和十六进制）常量及其运算
```
包含的运算符: 

所有SysY语言定义规定的合法常量表达式可能包含的运算符。

常量表达式在编译期间即可计算得到确定结果，LLVM IR 会自动计算出常量表达式的结果，所以你在实现时也需要直接返回正确的运算结果。

## 样例

### 样例一

输入：
```
int main() {
    return -2;
}
```
输出：
```
; ModuleID = 'module'
source_filename = "module"

define i32 @main() {
mainEntry:
  ret i32 -2
}
```
### 样例二

输入：
```
int main() {
    return 3 + 2 * 5;
}
```
输出：
```
; ModuleID = 'module'
source_filename = "module"

define i32 @main() {
mainEntry:
  ret i32 13
}
```
提醒：该部分 ModuleID 和 source_filename 的值可以自定义，其他请尽量与样例输出的格式一致，以免出错。

## Part 2 翻译局部变量和全局变量

- 该部分需要完成对变量的声明、定义和使用的翻译，要求如下
  - 函数仍然只包含 main 函数，无参数，返回 int 类型的值
  - 在 main 函数外还有全局变量的声明、定义和使用
  - main 函数除返回语句外还有局部变量的声明、定义和使用
  - 除 int 类型的变量外还需要翻译常量 const int
  - 变量也会参与表达式运算，运算也会涉及常量和字面量
  - 本次实验不要求翻译数组

- 说明
  - 局部变量的使用包括了许多种情况，包括：局部变量被赋值、一个局部变量赋值给另一个局部变量、局部变量作为 main 函数返回值等等
  - 对于 int 常量，它们的处理与 int 变量一致，也就是说它们可以被翻译成相同的LLVM IR。因为对于 const 的检查应该只发生于语义分析阶段（虽然我们目前没有定义过关于 const 的语义错误）
  - 全局变量（和常量）不会用于在 main 函数外赋值给全局变量

## 样例

### 样例一

输入：
```
int g_var = 2;
int main() {
    int a = 1;
    return a + g_var;
}
```
输出：
```
; ModuleID = 'module'
source_filename = "module"

@g_var = global i32 2

define i32 @main() {
mainEntry:
  %a = alloca i32, align 4
  store i32 1, i32* %a, align 4
  %a1 = load i32, i32* %a, align 4
  %g_var = load i32, i32* @g_var, align 4
  %tmp_ = add i32 %a1, %g_var
  ret i32 %tmp_
}
```

### 样例二

输入：
```
int main() {
    int a = 2;
    return a;
}
```
输出：
```
; ModuleID = 'moudle'
source_filename = "moudle"

define i32 @main() {
mainEntry:
  %a = alloca i32, align 4
  store i32 2, i32* %a, align 4
   %a1 = load i32, i32* %a, align 4
  ret i32 %a1
}
```

提醒：该部分不要求你输出的 IR 与样例中的完全一致，只要能正确执行并返回正确的结果即可。

## Part3 函数定义与调用

- 该部分需要完成对函数定义与函数调用的翻译
- 本次实验不要求翻译数组类型参数

## Part4 流程控制

- 该部分需要完成对各种流程控制相关语句的翻译，要求如下
    - 需要翻译if语句与if-else语句，注意if语句与if-else语句是可以嵌套的
    - 需要翻译while循环，同样可以嵌套
    - 需要翻译break和continue语句
    
      - break仅跳出最内层的while循环，不允许跳出特定层循环
      - continue语句将会跳过最内层while循环后续所有语句直接进行其条件判断
    - 需要实现逻辑表达式的短路机制！！！

## 样例
### 样例一

输入：
```
int f(int i) {
    return i;
}

int main() {
    int a = 1;
    return f(a);
}
```
输出：
```
; ModuleID = 'module'
source_filename = "module"

define i32 @f(i32 %0) {
fEntry:
  %i = alloca i32, align 4
  store i32 %0, i32* %i, align 4
  %i1 = load i32, i32* %i, align 4
  ret i32 %i1
}

define i32 @main() {
mainEntry:
  %a = alloca i32, align 4
  store i32 1, i32* %a, align 4
  %a1 = load i32, i32* %a, align 4
  %returnValue = call i32 @f(i32 %a1)
  ret i32 %returnValue
}
```
### 样例二

输入：
```
int a = 0;
int count = 0;
    
int main() {
    while (a <= 0) {
        a = a - 1;
        count = count + 1;
        if (a < -20)
            break;
    }
    return count;
}
```
输出：
```
; ModuleID = 'module'
source_filename = "module"

@a = global i32 0
@count = global i32 0

define i32 @main() {
mainEntry:
  br label %whileCond

whileCond:                                        ; preds = %if_next_, %mainEntry
  %tmp_ = load i32, i32* @a, align 4
  %cond_ = icmp sle i32 %tmp_, 0
  %cond_1 = zext i1 %cond_ to i32
  %cond_2 = icmp ne i32 %cond_1, 0
  br i1 %cond_2, label %whileBody, label %whileNext

whileBody:                                        ; preds = %whileCond
  %tmp_3 = load i32, i32* @a, align 4
  %tmp_4 = sub i32 %tmp_3, 1
  store i32 %tmp_4, i32* @a, align 4
  %tmp_5 = load i32, i32* @count, align 4
  %tmp_6 = add i32 %tmp_5, 1
  store i32 %tmp_6, i32* @count, align 4
  %tmp_7 = load i32, i32* @a, align 4
  %cond_8 = icmp slt i32 %tmp_7, -20
  %cond_9 = zext i1 %cond_8 to i32
  %cond_10 = icmp ne i32 %cond_9, 0
  br i1 %cond_10, label %if_true_, label %if_false_

whileNext:                                        ; preds = %if_true_, %whileCond
  %tmp_11 = load i32, i32* @count, align 4
  ret i32 %tmp_11

if_true_:                                         ; preds = %whileBody
  br label %whileNext

if_false_:                                        ; preds = %whileBody
  br label %if_next_

if_next_:                                         ; preds = %if_false_, %if_true_
  br label %whileCond
}
```
提醒

- 你输出的ir不一定要跟助教一致，只要能正确执行并返回正确的结果即可。

- 例如，对于样例二，你也可以生成如下的IR：
```
; ModuleID = 'module'
source_filename = "module"

@a = global i32 0
@count = global i32 0

define i32 @main() {
0:    
  %1 = load i32, ptr @a, align 4
  %2 = icmp sle i32 %1, 0
  br i1 %2, label %3, label %14

3:
  %4 = load i32, ptr @a, align 4
  %5 = load i32, ptr @count, align 4
  br label %6

6:
  %7 = phi i32 [ %5, %3 ], [ %10, %6 ]
  %8 = phi i32 [ %4, %3 ], [ %9, %6 ]
  %9 = add i32 %8, -1
  %10 = add i32 %7, 1
  %11 = icmp slt i32 %8, -20
  %12 = xor i1 %11, true
  br i1 %12, label %6, label %13

13:
  store i32 %9, ptr @a, align 4
  store i32 %10, ptr @count, align 4
  br label %14

14:
  %15 = load i32, ptr @count, align 4
  ret i32 %15
}
```
容易发现，上面的IR使用phi指令来合并来自不同前驱的值，这种方式简洁且有更少的内存开销。

### 样例三

输入：
```
int count = 0;
int n = 3;

void hanoi(int n, int source, int target, int auxiliary) {
    if (n == 1) {
        count = count + 1;
        return;
    }
    hanoi(n - 1, source, auxiliary, target);
    count = count + 1;
    hanoi(n - 1, auxiliary, target, source);
}

int main() {
    hanoi(n, 1, 3, 2);
    return count;
}
```
输出：
```
; ModuleID = 'module'
source_filename = "module"

@count = global i32 0
@n = global i32 3

define void @hanoi(i32 %0, i32 %1, i32 %2, i32 %3) {
hanoiEntry:
  %n = alloca i32, align 4
  store i32 %0, i32* %n, align 4
  %source = alloca i32, align 4
  store i32 %1, i32* %source, align 4
  %target = alloca i32, align 4
  store i32 %2, i32* %target, align 4
  %auxiliary = alloca i32, align 4
  store i32 %3, i32* %auxiliary, align 4
  %tmp_ = load i32, i32* %n, align 4
  %cond_ = icmp eq i32 %tmp_, 1
  %cond_1 = zext i1 %cond_ to i32
  %cond_2 = icmp ne i32 %cond_1, 0
  br i1 %cond_2, label %if_true_, label %if_false_

if_true_:                                         ; preds = %hanoiEntry
  %tmp_3 = load i32, i32* @count, align 4
  %tmp_4 = add i32 %tmp_3, 1
  store i32 %tmp_4, i32* @count, align 4
  ret void

if_false_:                                        ; preds = %hanoiEntry
  br label %if_next_

if_next_:                                         ; preds = %if_false_, %if_true_
  %tmp_5 = load i32, i32* %n, align 4
  %tmp_6 = sub i32 %tmp_5, 1
  %tmp_7 = load i32, i32* %source, align 4
  %tmp_8 = load i32, i32* %auxiliary, align 4
  %tmp_9 = load i32, i32* %target, align 4
  call void @hanoi(i32 %tmp_6, i32 %tmp_7, i32 %tmp_8, i32 %tmp_9)
  %tmp_10 = load i32, i32* @count, align 4
  %tmp_11 = add i32 %tmp_10, 1
  store i32 %tmp_11, i32* @count, align 4
  %tmp_12 = load i32, i32* %n, align 4
  %tmp_13 = sub i32 %tmp_12, 1
  %tmp_14 = load i32, i32* %auxiliary, align 4
  %tmp_15 = load i32, i32* %target, align 4
  %tmp_16 = load i32, i32* %source, align 4
  call void @hanoi(i32 %tmp_13, i32 %tmp_14, i32 %tmp_15, i32 %tmp_16)
  ret void
}

define i32 @main() {
mainEntry:
  %tmp_ = load i32, i32* @n, align 4
  call void @hanoi(i32 %tmp_, i32 1, i32 3, i32 2)
  %tmp_1 = load i32, i32* @count, align 4
  ret i32 %tmp_1
}
```
